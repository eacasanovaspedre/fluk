buildscript {

    ext {
        kotlin_version = '1.1.2-4'
    }

    repositories {
        maven {
            url "https://oss.sonatype.org"
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

def major = 1
def minor = 2

enum ReleaseType {
    RELEASE(""),
    SNAPSHOT("-SNAPSHOT"),
    BETA("-BETA")

    public final String value;

    ReleaseType(String value) {
        this.value = value
    }
}

static def buildVersion(int major, int minor, ReleaseType releaseType) {
    return "$major.$minor${releaseType.value}"
}

group = "com.github.eacasanovaspedre.fluk "
version = buildVersion(major, minor, ReleaseType.SNAPSHOT)

def bumpVersion(int minor) {
    def declaration = "def minor = "
    def buildFile = new File("build.gradle")
    def nextMinor = minor
    buildFile.text.find ~/$declaration($minor)/, { match, minorVersion ->
        nextMinor = Integer.valueOf((String) minorVersion) + 1
    }
    print "Increasing minor version from $minor to $nextMinor..."
    buildFile.text = buildFile.text.replace("$declaration$minor", "$declaration$nextMinor")
    println "Done"
}

task "prepare" doFirst {
    version = buildVersion(major, minor, ReleaseType.RELEASE)
}

task "beta" doFirst {
    version = buildVersion(major, minor, ReleaseType.BETA)
}

task "publish" {
    dependsOn "uploadArchives"
} doLast {
    println "Published version $version"
    bumpVersion(minor)
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Fluk'
                packaging 'jar'
                description 'A library for making Kotlin more functional friendly'
                url 'https://github.com/eacasanovaspedre/fluk'

                scm {
                    connection 'scm:git:https://github.com/eacasanovaspedre/fluk.git'
                    developerConnection 'scm:git:https://github.com/eacasanovaspedre/fluk.git'
                    url 'https://github.com/eacasanovaspedre/fluk'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'eacasanovaspedre'
                        name 'Enrique Alfonso Casanovas Pedre'
                        email 'eacasanovaspedre@yandex.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.4.1"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}